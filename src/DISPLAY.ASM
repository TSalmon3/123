$INCLUDE(REG52.INC)
$INCLUDE(SETTING.INC)
NAME DISPLAY

PUBLIC DISP

DISP?DISPLAY SEGMENT CODE
    
;--------------------------------------------------------------------------------------------------------
;功能：动态显示
;占用：R1, R0, A, DPTR
;返回：无
;--------------------------------------------------------------------------------------------------------
RSEG DISP?DISPLAY
DISP:
MOV R1, #01H;第一块数码管位码（最右边）
MOV R0, #30H;指向显存中第一个数据（索引）
DI1:
	MOV A, R1;取位码
	MOV DPTR, #(0FE00H OR _8255A_PORTB);指向8255A的B口
	MOVX @DPTR, A;输出位码

	MOV DPTR, #STAB;指向译码表
	MOV A, @R0;取显存数据
	MOVC A, @A+DPTR;取的段码
	MOV DPTR, #(0FE00H OR _8255A_PORTA);指向8255芯片的A口
	MOVX @DPTR, A;输出段码

	LCALL D1MS;保持显示1MS

	MOV A, #0FFH ;取空格码
	MOV DPTR, #(0FE00H OR _8255A_PORTA);指向8255芯片的A口
	MOVX @DPTR, A ;输出空格码

	INC R0;指向指向显存中下一个数据
	MOV A, R1
	RL A 
	MOV R1, A;取下一块数码管位码 

JNB ACC.0, DI1;如果还没送完显存中最后一个数据继续送下一个
RET


;段码表
STAB:DB 0C0H,0F9H,0A4H,0B0H,99H,92H,82H	;字段码表： "0"～"6" 
	DB 0F8H,80H,90H,88H,83H,0C6H,0A1H	;字段码表续："7"～"9"，"A"～"D"   
	DB 86H,8EH,0BFH,00H,0FFH		;字段码表续："E","F","-",全亮,空格
	DB 40H,079H,024H,30H,19H,12H,02H ;字段码表： "0."～"6." 
	DB 78H,00H,10H					;字段码表续："7."～"9."


;--------------------------------------------------------------------------------------------------------
;功能：延时1ms
;占用：R7
;返回：无
;--------------------------------------------------------------------------------------------------------
D1MS:
PUSH 07H
MOV R7,#115         	;软件延时1mS子程序
  D1MS1:NOP			;(1+8*115+2)*12/11059.2KHz=1.0015mS
	NOP       
	NOP
	NOP
	NOP
	NOP
	DJNZ R7,D1MS1
POP 07H
RET

END
